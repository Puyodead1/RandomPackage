import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

configure(subprojects) {
    apply plugin: 'maven'
    apply plugin: 'com.github.johnrengelman.shadow'
    configurations {
        create("shade")
        getByName("archives").extendsFrom(getByName("default"))
    }

    group = rootProject.group + ".randompackage-libs"

    tasks.register("jar", ShadowJar) {
        configurations = [project.configurations.shade]
        classifier = ""

        dependencies {
            exclude(dependency("com.google.guava:guava"))
            exclude(dependency("com.google.code.gson:gson"))
            exclude(dependency("org.checkerframework:checker-qual"))
        }
    }

    def altConfigFiles = { String artifactType ->
        def deps = configurations.shade.incoming.dependencies
                .collect { it.copy() }
                .collect { dependency ->
                    dependency.artifact { artifact ->
                        artifact.name = dependency.name
                        artifact.type = artifactType
                        artifact.extension = 'jar'
                        artifact.classifier = artifactType
                    }
                    dependency
                }

        return files(configurations.detachedConfiguration(deps as Dependency[])
                .resolvedConfiguration.lenientConfiguration.getArtifacts()
                .findAll { it.classifier == artifactType }
                .collect { zipTree(it.file) })
    }

    tasks.register("sourcesJar", Jar) {
        from {
            altConfigFiles('sources')
        }

        classifier = "sources"
    }

    artifacts {
        add("default", jar)
        add("archives", sourcesJar)
    }

    tasks.register("install", Upload) {
        configuration = configurations.archives
        repositories.mavenInstaller {
            pom.version = project.version
            pom.artifactId = project.name
        }
    }

    build.dependsOn(jar, sourcesJar)
}

project("spigot") {
    repositories {
        maven {
            name = "SpigotMC"
            url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
        }
    }
}

project("sponge") {
    repositories {
        maven {
            name = "Sponge"
            url = "https://repo.spongepowered.org/maven"
        }
    }
}

tasks.register("build") {
    dependsOn(subprojects.collect { it.tasks.named("build") })
}
